{
  config,
  pkgs,
  lib,
  ...
}:

let
  hermesConfigYaml = pkgs.writeText "hermes-config.yaml" ''
    model:
      default: hermes3:8b
      provider: custom
      base_url: http://host.containers.internal:11434/v1
      context_length: 65536
    terminal:
      env: local
  '';
in
{
  sops.secrets."hermes-agent/telegram-bot-token" = { };
  sops.secrets."hermes-agent/telegram-allowed-users" = { };
  sops.secrets."hermes-agent/dashboard-username" = { };
  sops.secrets."hermes-agent/dashboard-password" = { };
  sops.secrets."hermes-agent/api-server-key" = { };

  sops.templates."hermes-agent-env" = {
    mode = "0400";
    content = ''
      TELEGRAM_BOT_TOKEN=${config.sops.placeholder."hermes-agent/telegram-bot-token"}
      TELEGRAM_ALLOWED_USERS=${config.sops.placeholder."hermes-agent/telegram-allowed-users"}
      HERMES_DASHBOARD_BASIC_AUTH_USERNAME=${config.sops.placeholder."hermes-agent/dashboard-username"}
      HERMES_DASHBOARD_BASIC_AUTH_PASSWORD=${config.sops.placeholder."hermes-agent/dashboard-password"}
      API_SERVER_KEY=${config.sops.placeholder."hermes-agent/api-server-key"}
    '';
  };

  systemd.tmpfiles.rules = [
    "d /var/lib/hermes-agent 0755 root root -"
  ];

  # Create isolated Podman network for Hermes
  systemd.services.hermes-network-init = {
    description = "Create Hermes Agent Podman network";
    wantedBy = [ "multi-user.target" ];
    before = [ "podman-hermes-agent.service" ];
    after = [ "podman.service" ];
    serviceConfig = {
      Type = "oneshot";
      RemainAfterExit = true;
    };
    path = [ config.virtualisation.podman.package ];
    script = ''
      if ! podman network exists hermes-net 2>/dev/null; then
        podman network create hermes-net --subnet 10.89.0.0/24 --gateway 10.89.0.1
      fi
    '';
  };

  systemd.services.hermes-agent-init = {
    description = "Deploy Hermes Agent config.yaml";
    wantedBy = [ "multi-user.target" ];
    before = [ "podman-hermes-agent.service" ];
    serviceConfig = {
      Type = "oneshot";
      RemainAfterExit = true;
    };
    script = ''
      cp ${hermesConfigYaml} /var/lib/hermes-agent/config.yaml
      chmod 644 /var/lib/hermes-agent/config.yaml
    '';
  };

  # Proxy dashboard to tailscale interface only
  systemd.services.hermes-dashboard-proxy = {
    description = "Proxy Hermes dashboard to Tailscale interface";
    after = [
      "network-online.target"
      "tailscaled.service"
      "podman-hermes-agent.service"
    ];
    wants = [ "network-online.target" ];
    wantedBy = [ "multi-user.target" ];
    path = [ pkgs.socat pkgs.tailscale ];
    serviceConfig = {
      ExecStart = pkgs.writeShellScript "hermes-dashboard-proxy" ''
        TS_IP=$(tailscale ip -4)
        exec socat TCP-LISTEN:9119,bind="$TS_IP",reuseaddr,fork TCP:127.0.0.1:9119
      '';
      Restart = "on-failure";
      RestartSec = 5;
    };
  };

  virtualisation.oci-containers.containers.hermes-agent = {
    image = "docker.io/nousresearch/hermes-agent:latest";
    autoStart = true;
    extraOptions = [
      "--network=hermes-net"
      "--ip=10.89.0.2"
      "--add-host=host.containers.internal:host-gateway"
      "-p" "127.0.0.1:9119:9119"
      "-p" "127.0.0.1:8642:8642"
      "--cap-drop=ALL"
      "--cap-add=DAC_OVERRIDE"
      "--cap-add=CHOWN"
      "--cap-add=FOWNER"
      "--cap-add=SETUID"
      "--cap-add=SETGID"
      "--security-opt=no-new-privileges"
      "--pids-limit=256"
    ];
    volumes = [
      "/var/lib/hermes-agent:/opt/data"
    ];
    environmentFiles = [
      config.sops.templates."hermes-agent-env".path
    ];
    environment = {
      HERMES_HOME = "/opt/data";
      HERMES_WRITE_SAFE_ROOT = "/opt/data";
      HERMES_DASHBOARD = "1";
      HERMES_TIMEZONE = "America/Denver";
      HERMES_REDACT_SECRETS = "true";
      API_SERVER_ENABLED = "true";
      API_SERVER_HOST = "0.0.0.0";
    };
    cmd = [
      "gateway"
      "run"
    ];
  };

  networking.firewall.interfaces.tailscale0.allowedTCPPorts = [ 9119 ];
}
